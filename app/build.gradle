plugins {
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.0.0-M2'
}

apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/versioning.gradle"

sourceCompatibility = 11
targetCompatibility = 11

group = 'io.github.sogis'
def artifactId = "pdf4oereb"

repositories {
    mavenCentral()
}

configurations {
    jar.archiveName = "pdf4oereb-"+version+".jar"
}

// "The package javax.xml.transform.stream is accessible from more than one module: <unnamed>, java.xml"
configurations {
    all*.exclude group: 'xml-apis'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'net.sf.saxon:Saxon-HE:10.6'
    implementation 'org.apache.xmlgraphics:fop:2.7'
    implementation 'net.sf.offo:fop-hyph:2.0' 
    implementation 'org.locationtech.jts:jts-core:1.18.2'
}

testing {
    suites {
        test {
            useJUnitJupiter('5.8.1')
        }
    }
}

distZip {
    archiveName 'pdf4oereb' + '-'  + project.version.toString() + '.zip'
}

distTar {
    archiveName 'pdf4oereb' + '-'  + project.version.toString() + '.gzip'
}

tasks.distTar {
    compression = Compression.GZIP
}

application {
    mainClass = 'ch.so.agi.oereb.pdf4oereb.App'
}

startScripts {
   applicationName = 'pdf4oereb'
}

run {
    //args = ["--xml", "src/test/data/ne/20220125_70_2364_geom_images.xml", "--out", "/Users/stefan/tmp/pdf4oereb", "--lang", "de"]          
    args = ["--xml", "src/test/data/bl/CH175978021788_geom_images.xml", "--out", "/Users/stefan/tmp/pdf4oereb", "--lang", "de"]          
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName 'pdf4oereb'
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName 'pdf4oereb'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = 'io.github.sogis'
            artifactId = 'pdf4oereb'

            pom {
                name = "pdf4oereb"
                url = "https://github.com/sogis/pdf4oereb"
                description = "pdf4oereb"
                scm {
                    connection = "scm:git:https@github.com/sogis/pdf4oereb"
                    developerConnection = "scm:git:https@github.com/sogis/pdf4oereb"
                    url = "https://github.com/sogis/pdf4oereb"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/sogis/pdf4oereb/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "edigonzales"
                        name = "Stefan Ziegler"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jreleaser {
    gitRootSearch = true
    
    project {
        name = 'pdf4oereb'
        description = 'pdf4oereb transforms a PLR DATA-Extract XML document version 2 into the corresponding PDF document.'
        website = 'https://agi.so.ch'
        authors = ['edigonzales']
        license = 'MIT'
        licenseUrl = 'https://opensource.org/licenses/MIT'
        docsUrl = 'https://github.com/sogis/pdf4oereb'
        copyright = '2022 AGI Kanton Solothurn'
    }
    release {
        github {
            owner = 'sogis'
            overwrite = true
            apiEndpoint = 'https://api.github.com'
        }
    }
    distributions {
        app {
            distributionType = 'JAVA_BINARY'
            artifact {
                path = "build/distributions/pdf4oereb-${version}.zip"
            }
            java {
                groupId = 'io.github.sogis'
                artifactId = 'pdf4oereb'
                version = '11'
            }
        }
    }
}
